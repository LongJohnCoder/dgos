# Yeah I know this duplicates remake
# If I try to use remake at startup, gdb crashes

set architecture i8086
symbol-file $SYMBOLFILE
set architecture i386:x86-64
set radix 16
target remote | exec qemu-system-x86_64 -m 6G -S -drive file=$DISKIMAGE,format=raw -gdb stdio -no-shutdown -no-reboot
#-d int,cpu_reset
set architecture i8086
shell wmctrl -a kernel/boot
layout src
b debug_break

define remake
	kill inferior 1
	make debuggable_disk
	refresh
	set architecture i386:x86-64
	target remote | exec qemu-system-x86_64 -m 6G -S -drive file=$DISKIMAGE,format=raw -gdb stdio -no-shutdown -no-reboot
	#-d int,cpu_reset
	set architecture i8086
	shell wmctrl -a kernel/boot
end

document remake
Kills the inferior,
runs make debuggable_disk,
refreshes the window,
starts the inferior,
and sets focus back to GDB
end

define qq
	kill inferior 1
	quit
end

document qq
Kills the inferior and quits, without prompting
end

define code16
	set architecture i8086
	refresh
end

define code32
	set architecture i386
	refresh
end

define code64
	set architecture i386:x86-64
	refresh
end

b elf64_run
